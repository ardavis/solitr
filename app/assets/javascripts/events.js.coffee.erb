#= require jquery
#= require jquery.cookie
#= require helpers
#= require services/ga

App.commit = <%= (File.read('.commit').presence || `git rev-parse HEAD`.presence || 'unknown')[0...10].to_json %>
p 'Solitr version', App.commit

class App.Events
  @_firebase: ->
    # Returns undefined if Firebase or GA is not yet loaded.
    if @_visitorId and Firebase?
      try
        @_firebaseRef ?= \
          new Firebase('https://solitr-analytics.firebaseio.com')
            .child(@_visitorId).child(App.commit)
      catch error

  @_trackEvent: (args...) ->
    p '_trackEvent', args...
    try
      _gaq.push(['_trackEvent', args...])
    catch error

  @_metricTotals: {}

  @_addToMetric: (metricName, x) ->
    p 'Adding', metricName, x
    @_metricTotals[metricName] ||= 0
    @_metricTotals[metricName] += x
    @_flushMetric(metricName)

  @_metricRetryTimers: {}

  @_flushMetric: (metricName) ->
    if firebase = @_firebase()
      p 'Flushing', metricName, @_metricTotals[metricName]
      if @_metricRetryTimers[metricName]?
        clearInterval @_metricRetryTimers[metricName]
        @_metricRetryTimers[metricName] = null
      try
        metricRef = firebase.child(metricName)
        @_metricUniqueId ?= metricRef.push().name()
        metricRef.child(@_metricUniqueId).set(@_metricTotals[metricName])
      catch error
    else
      p 'Could not flush metrics, retrying soon'
      @_metricRetryTimers[metricName] ?= setInterval(=>
        @_flushMetric(metricName)
      , 500)

  @beginGame: ->
    @_trackEvent('game', 'begin')
    @_addToMetric('beginGame', 1)
    @_addToMetric('chi', 0.5)

  @stayOnSite: ->
    # Did not bounce
    @_trackEvent('general', 'stay')
    @_addToMetric('stayOnSite', 1)
    @_addToMetric('chi', 50)

  @activeBeacon: ->
    minutes = @_activeBeaconInterval / 60
    @_trackEvent('general', 'active')
    @_addToMetric('active', minutes)
    @_addToMetric('chi', minutes)

  @initialVisit: not ($.cookie('__utmb') and $.cookie('__utmc'))

  # Call stayOnSite after 30 seconds, even across reloads.
  @scheduleInitialVisitBeacon: ->
    cookieOptions = (expiresInSeconds) ->
      opts =
        path: '/'
        <% if Rails.env.production? %>domain: 'solitr.com'<% end %>
      if expiresInSeconds
        opts.expires = new Date(Date.now() + expiresInSeconds * 1000)
      opts

    beaconDelay = 30

    if App.Events.initialVisit
      $.cookie 'solitr_initial_short', '_', cookieOptions(beaconDelay)
      $.cookie 'solitr_initial_long', '_', cookieOptions(60 * 30)

    # We wait for the short cookie to disappear while the long cookie is still
    # around. The long cookie is used to ensure that we still send a beacon
    # even if the short cookie expires during a page reload.
    #
    # If no `initial` cookies are around, this will do nothing.
    intervalId = setInterval ->
      if $.cookie('solitr_initial_long')
        if not $.cookie('solitr_initial_short')
          # p 'No short cookie'
          removeCookies()
          App.Events.stayOnSite()
        # else
        #   p 'Waiting for short cookie to disappear'
      else
        # p 'No long cookie'
        removeCookies()
    , 1000

    removeCookies = ->
      $.removeCookie('solitr_initial_long', cookieOptions())
      $.removeCookie('solitr_initial_short', cookieOptions())
      # Clear interval just in case we didn't remove the cookies successfully.
      clearInterval intervalId

  @_activeBeaconInterval: 60
  @scheduleActiveBeacon: ->
    idleTimeout = 30
    $ =>
      $.idleTimer(idleTimeout * 1000)
      setInterval ->
        if $(document).data('idleTimer') == 'active'
          App.Events.activeBeacon()
      , @_activeBeaconInterval * 1000

<% if Rails.env.development? %>
App.Events._visitorId = 1
p 'Self-assigning visitor ID 1'
<% elsif Rails.env.production? %>
try
  _gaq.push ->
    App.Events._visitorId = _gat._getTrackers()[0]._visitCode()
    p 'Visitor ID', App.Events._visitorId
catch error
<% end %>

<% unless Rails.env.test? %> # No non-deterministic business in tests
App.Events.scheduleInitialVisitBeacon()
App.Events.scheduleActiveBeacon()
<% end %>
